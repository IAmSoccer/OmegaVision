plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.1'
}

group = 'me.omegaweapondev'
version = '2.5.14'

repositories {
    mavenLocal()
    maven {
        url = uri('https://hub.spigotmc.org/nexus/content/groups/public/')
    }

    maven {
        url = uri('https://repo.codemc.org/repository/maven-public')
    }

    maven {
        url = uri('https://repo.extendedclip.com/content/repositories/placeholderapi/')
    }

    maven {
        url = uri('https://repo.maven.apache.org/maven2/')
    }

    maven {
        url = uri('https://jitpack.io')
    }
    mavenCentral()
}
dependencies {
    implementation "me.ou:library:1.6.4"
    implementation 'org.bstats:bstats-bukkit:2.2.1'
    compileOnly 'org.spigotmc:spigot-api:1.18-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.11.0'
    compileOnly 'org.jetbrains:annotations:22.0.0'
    compileOnly 'com.github.MilkBowl:VaultAPI:1.7'
}

sourceCompatibility = 11
targetCompatibility = 11

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.processResources {
    filesMatching("plugin.yml") {
        expand(
                "name": project.name,
                "version": project.version,
                "mainClass": project.group + ".omegavision.OmegaVision",
                "apiVersion": 1.13,
                "author": 'OmegaWeaponDev'
        )
    }
}

shadowJar {
    archiveClassifier.set('')
    relocate 'me.ou', 'me.omegaweapondev.libs.ou'
    relocate 'org.bstats', 'me.omegaweapondev.libs.bstats'
    relocate 'net.kyori', 'me.omegaweapondev.libs.adventure'
    relocate 'com.tchristofferson', 'me.omegaweapondev.libs.configupdater'
    relocate 'dev.dbassett', 'me.omegaweapondev.libs.skullcreator'
}



